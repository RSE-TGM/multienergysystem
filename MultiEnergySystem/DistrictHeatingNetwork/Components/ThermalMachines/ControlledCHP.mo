within MultiEnergySystem.DistrictHeatingNetwork.Components.ThermalMachines;
model ControlledCHP "Model of an ideal controlled CHP"
  extends MultiEnergySystem.DistrictHeatingNetwork.Components.ThermalMachines.BaseClass.PartialBoiler;

  // Generic Gas model
  replaceable model Gas = MultiEnergySystem.H2GasFacility.Media.IdealGases.NG_4 constrainedby MultiEnergySystem.H2GasFacility.Media.BaseClasses.PartialMixture;

  // Parameters
  parameter DistrictHeatingNetwork.Types.MassFlowRate m_flow_fuel_nom = 0.004 "Nominal fuel (CH4) mass flow rate" annotation (
    Dialog(tab = "Combustion Data"));
  parameter Real HH(unit = "J/kg", nominal = 10e6) = 50e6 "Nominal fuel calorific power" annotation (
    Dialog(tab = "Combustion Data"));
  parameter DistrictHeatingNetwork.Types.Temperature Tout_ref = 80 + 273.15 "Reference value for internal control";
  parameter SI.Time tdelay = 10 "Delay time to obtain 100% thermal power";
  parameter SI.Time tau_el = 10 "Time constant of electric power first order response";
  parameter DistrictHeatingNetwork.Types.PerUnit eta_el_nom = 0.3448 "Nominal electrical efficiency";
  parameter DistrictHeatingNetwork.Types.PerUnit eta_th_nom = 0.5453 "Nominal thermal efficiency" annotation (
    Dialog(tab = "Combustion Data"));
  parameter DistrictHeatingNetwork.Types.Power Pel_nom = eta_el_nom*Pnom "Electrical Nominal Power" annotation (
    Dialog(tab = "Nominal Data"));
  parameter DistrictHeatingNetwork.Types.Power Pth_nom = eta_th_nom*Pnom "Thermal Nominal Power" annotation (
    Dialog(tab = "Nominal Data"));

  // Variables
  DistrictHeatingNetwork.Types.MassFlowRate m_flow_fuel "mass flowrate of the motor fuel";
  DistrictHeatingNetwork.Types.Power Pheat_in;
  DistrictHeatingNetwork.Types.Power Pheat_ref "Reference value for computed Heat Power required";
  DistrictHeatingNetwork.Types.SpecificEnthalpy hout_ref "Reference required temperature";
  Medium fluidOut_ref(T_start = Tout_start, p_start = pout_start) "Reference outlet fluid";
  Gas fuel(T_start = 15 + 273.15, p_start = 1.013e5) "Reference gas fluid";



  // Inputs
  Modelica.Blocks.Interfaces.RealInput Pel_ref "Electric power reference (set-point) request to the CHP" annotation (Placement(transformation(extent={{-80,-10},{-60,10}}), iconTransformation(extent={{-80,-10},{-60,10}})));
  Modelica.Blocks.Interfaces.BooleanInput heat_on "ON/OFF for the operation of the thermal side in the CHP" annotation (Placement(
        transformation(extent={{-80,-60},{-60,-40}}),
                                                    iconTransformation(extent={{-80,-60},{-60,-40}})));

  // Fluid port
  H2GasFacility.Interfaces.FluidPortInlet inletfuel(nXi=fuel.nXi) "Inlet connector for the motor fuel" annotation (Placement(visible = true, transformation(origin={0,-100},   extent = {{-10, -10}, {10, 10}}, rotation = 0), iconTransformation(origin={70,0},    extent={{-10,-10},{10,10}},
                      rotation = 0)));

  // Outputs
  Modelica.Blocks.Interfaces.RealOutput Pel_actual "Actual output electric power generated by the CHP" annotation (Placement(transformation(extent={{60,-56},{72,-44}}), iconTransformation(extent={{60,-56},{72,-44}})));
equation
  // Momentum balance
  inlet.p - outlet.p = homotopy(m_flow*(449.449473 + m_flow*(14.618729 + 2.739099*m_flow)), pin_start - pout_start)  "Momentum Balance";

  // Fluid properties
  fluidOut_ref.p = pout;
  fluidOut_ref.T = Tout_ref;
  hout_ref = fluidOut_ref.h;
  0 =inlet.m_flow*(-hout_ref + hin) + Pheat_ref;

  // Power calculations
  Pheat_in =Pel_ref*eta_th_nom/eta_el_nom;
  Pheat = delay(if heat_on then min(Pheat_ref, Pheat_in) else 0, tdelay);

  // Fuel flow calculations
  Pheat = m_flow_fuel*HH*etanom;
  inletfuel.h_out = 0 "Dummy equation considering not fuel flow reversal";
  inletfuel.Xi = fuel.Xi_start "Dummy equation considering not fuel flow reversal";
  fuel.h = inStream(inletfuel.h_out);
  fuel.Xi = inStream(inletfuel.Xi);
  fuel.p = 1.013e5;
  m_flow_fuel = inletfuel.m_flow;

  // First-order response for electric power
  tau_el * der(Pel_actual) + Pel_actual = Pel_ref;

initial equation
  der(Pel_actual) = 0;

annotation (
    Icon(graphics={  Polygon(origin={-1,3},    lineColor = {255, 0, 0}, fillColor = {255, 0, 0}, fillPattern = FillPattern.Horizontal, points = {{-21, -37}, {-27, -3}, {-21, -13}, {-19, 25}, {-11, 13}, {1, 37}, {13, 13}, {19, 25}, {23, -15}, {27, -5}, {21, -37}, {1, -43}, {-21, -37}}), Polygon(origin={-1,3},    lineColor = {255, 0, 0}, fillColor = {255, 255, 0}, fillPattern = FillPattern.Solid, points = {{-15, -37}, {-23, -13}, {-15, -17}, {-15, 3}, {-9, -1}, {1, 25}, {9, -1}, {15, 3}, {17, -17}, {23, -13}, {15, -37}, {1, -43}, {-15, -37}})}),
      Documentation(info="<html>
<p><span style=\"font-family: Arial; font-size: 12pt;\">The following model includes in its definition three different domains:</span></p>
<p><br><b><span style=\"font-family: Arial; font-size: 14pt;\">Thermal</span></b></p>
<p><span style=\"font-family: Arial; font-size: 12pt;\">Two water connectors are included, which comes from the extended class <a href=\"modelica://MultiEnergySystem.DistrictHeatingNetwork.Components.ThermalMachines.BaseClass.PartialBoiler\">PartialBoiler</a>, representing the inlet and outlet connectors of the water to be heated in the CHP. </span></p>
<p><br><b><span style=\"font-family: Arial; font-size: 14pt;\">Gas</span></b></p>
<p><span style=\"font-family: Arial; font-size: 12pt;\">One new connector inletGas to represent the inlet for the motor fuel. In this model, the motor is not explicitely represented, then the fuel mass flow rate is already computed, so this connector should be attached to another model using also gas as fluid but with a source-pressure type behaviour, e.g. <b>SourcePressure.</span></b></p>
</html>"));
end ControlledCHP;
